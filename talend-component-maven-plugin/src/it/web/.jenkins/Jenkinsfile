//
//  Copyright (C) 2006-2022 Talend Inc. - www.talend.com
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//

/**
 * job-dsl plugin page api documentation for this project
 * https://jenkins-connectors.datapwn.com/plugin/job-dsl/api-viewer/index.html
 */

/**
 * Pod configuration
 */
final String tsbiImage = 'jdk11-svc-springboot-builder'
final String tsbiVersion = '2.9.18-2.4-20220104141654'
final String stageDefaultContainer = tsbiImage
final String podLabel = ((String) "tck-api-test-${UUID.randomUUID().toString()}").take(53)

// Local folder location
final String install_dir="webtester/install"
final String download_dir="webtester/download" // volume mounted on the podConfiguration
final String distribution_dir="${install_dir}/component-server-distribution"
final String artifact1="**/src/it/web/test/target/**/*"
final String artifact2="**/${install_dir}/coverage/**/*"
final String artifact3="**/${distribution_dir}/logs/**/*"

final String podConfiguration = """
  apiVersion: v1
  kind: Pod
  spec:
    containers:
      - name: '${tsbiImage}'
        image: 'artifactory.datapwn.com/tlnd-docker-dev/talend/common/tsbi/${tsbiImage}:${tsbiVersion}'
        command: [ cat ]
        tty: true
        volumeMounts: [
          { name: docker, mountPath: /var/run/docker.sock },
          { name: efs-jenkins-component-runtime-m2, mountPath: /root/.m2/repository },
          { name: efs-jenkins-component-runtime-download, mountPath: ${download_dir}},
          { name: dockercache, mountPath: /root/.dockercache }
        ]
        resources: { requests: { memory: 3G, cpu: '2' }, limits: { memory: 8G, cpu: '2' }}
    volumes:
      - name: docker
        hostPath: { path: /var/run/docker.sock }
      - name: dockercache
        hostPath: { path: /tmp/jenkins/tdi/docker }
      - name: efs-jenkins-component-runtime-m2
        persistentVolumeClaim:
          claimName: efs-jenkins-component-runtime-m2
      - name: efs-jenkins-component-runtime-download
        persistentVolumeClaim:
          claimName: efs-jenkins-component-runtime-download
    imagePullSecrets:
      - name: talend-registry
"""

/**
 * Credentials
 */

final def nexusCredentials = usernamePassword(
    credentialsId: 'nexus-artifact-zl-credentials',
    usernameVariable: 'NEXUS_USER',
    passwordVariable: 'NEXUS_PASSWORD')

final def tenantId_Rd = string(
    credentialsId: 'component-runtime-api-tenant-id-rd',
    variable: 'componentRuntimeApiTenantId_Rd'
)

/**
 * Options
 */

final String serverPort = "8081" //TODO: Find a available port to avoid conflicts

// Default value for parameters
String runtimeVersion = "default"
String apiTesterEnv = "component_runtime_ci"
String fileToRun = "tcomp_approved"
String tenantInstance = "eu"

/**
 * The pipeline
 */
pipeline {
  /**
   * agent
   */
  agent {
    kubernetes {
      label podLabel
      yaml podConfiguration
      defaultContainer stageDefaultContainer
    }
  }
  /**
   * parameters
   */
  parameters {
    string(
      name: 'CYCLE',
      defaultValue: 'default',
      description: 'Test Cycle Id filled by Test Orchestrator, it is only important for TTO.'
    )
    string(
      name: 'RUNTIME_VERSION',
      defaultValue: 'default',
      description: 'Choose the component-runtime.version to be used from Nexus. Keep default to use the pom version (1.45.0 minimum) or an other ex: 1.45.0-SNAPSHOT'
    )
    choice(
      name: 'PROJECT_NAME' ,
      choices: ['tcomp_approved', 'tcomp_draft'] ,
      description: 'Select test project to execute.'
    )
  }
  /**
   * environment
   */
  environment {
    APP_ID = '579232'
    TALEND_REGISTRY = "artifactory.datapwn.com"
    TESTCONTAINERS_HUB_IMAGE_NAME_PREFIX = "artifactory.datapwn.com/docker-io-remote/"

    // Locale storage of global var TODO: move it to somewhere where it is understood by the Intellij parser
    String workspace = "${WORKSPACE}"
    scripts_path = "${workspace}/talend-component-maven-plugin/src/it/web/.jenkins/scripts"
    tests_path = "${workspace}/talend-component-maven-plugin/src/it/web/test"
    settings_path = "${workspace}/talend-component-maven-plugin/src/it/web/.jenkins/settings.xml"
    build_user = "${BUILD_USER}"

    MAVEN_SETTINGS = "${settings_path}"
    DECRYPTER_ARG = "--define talend.maven.decrypter.m2.location=${workspace}/.jenkins/"
    MAVEN_OPTS = [
      "-Dmaven.artifact.threads=128",
      "-Dorg.slf4j.simpleLogger.showDateTime=true",
      "-Dorg.slf4j.simpleLogger.showThreadName=true",
      "-Dorg.slf4j.simpleLogger.dateTimeFormat=HH:mm:ss",
      "-Dtalend-image.layersCacheDirectory=/root/.dockercache"
    ].join(' ')

  }
  /**
   * options
   */
  options {
    buildDiscarder(logRotator(artifactNumToKeepStr: '30', numToKeepStr: '30'))
    timeout(time: 180, unit: 'MINUTES')
    skipStagesAfterUnstable()
  }
  /**
   * stages
   */
  stages {
    stage('Validate parameters') {
      steps {
          ///////////////////////////////////////////
          // Get the user name
          ///////////////////////////////////////////
          wrap([$class: 'BuildUser']) {
            script {
              try {
                USER_NAME = "${build_user}"
              }
              catch (groovy.lang.MissingPropertyException ignored) {
                USER_NAME = "auto"
              }
            }
          }

          ///////////////////////////////////////////
          // Validate parameters
          ///////////////////////////////////////////
          script {

            println "Setup all script as executables in ${scripts_path}\n"
            sh """
              find ${scripts_path} -type f -iname "*.sh" -exec chmod +x {} \\;
            """

            // Get actual tck version

            println "Get actual tck version from ${workspace}/pom.xml: \n"
            TCOMP_VERSION = sh (
              script: "${scripts_path}/pom-get-version.sh ${workspace}/pom.xml",
              returnStdout: true
            ).trim()

            println "TCOMP_VERSION on the pom file is ${TCOMP_VERSION}\n"

            // RUNTIME_VERSION
            try {
              runtimeVersion = "${RUNTIME_VERSION}"
            }
            catch (groovy.lang.MissingPropertyException ignored) {
              runtimeVersion = "default"
            }

            println "Requested TCOMP_VERSION is ${runtimeVersion}\n"

            if (runtimeVersion == "default") {
              runtimeVersion = "${TCOMP_VERSION}"
              println "RUNTIME_VERSION not supplied, local value will be used (${runtimeVersion})\n"
            }

            // FILE_TO_RUN
            try {
              fileToRun = "${PROJECT_NAME}"
            }
            catch (groovy.lang.MissingPropertyException ignored) {
              println "PROJECT_NAME not supplied, default value will be used (${fileToRun})\n"
            }
          }
          ///////////////////////////////////////////
          // Print build config
          ///////////////////////////////////////////
          script {

            println("Job info:\n" +
                " - Tenant instance: ${tenantInstance}\n" +
                " - Runtime version: ${runtimeVersion}\n" +
                " - Test file executed: ${fileToRun}" + ".json\n" +
                " - API Tester used environment: ${apiTesterEnv}\n")

            println("Talend Test Orchestrator info:\n" +
                "PLATFORM: ${params.PLATFORM}\n" +
                "ENVIRONMENT: ${params.ENVIRONMENT}\n" +
                "FRAGMENT_URL: ${params.FRAGMENT_URL}\n" +
                "DEPLOYMENT_UNIT: ${params.DEPLOYMENT_UNIT}\n" +
                "CYCLE: ${params.CYCLE}\n" +
                "DEPLOYED_PRODUCTS: ${params.DEPLOYED_PRODUCTS}")

            // updating build displayName
            currentBuild.displayName = (
                "#" + currentBuild.number + ": " + fileToRun
            )

            // updating build description
            currentBuild.description = (
                "User: " + "${USER_NAME}" + " - " + "Runtime: " + runtimeVersion + "\n" +
                    "Project: " + fileToRun + "\n"
            )
          }

      }
    }
    stage('Prepare Test') {
      steps {
        withCredentials([nexusCredentials]) {
          script {
            sh """
              if [[ ${runtimeVersion} == *"-SNAPSHOT" ]]; then
                echo "# Build a snapshot version"
                "${scripts_path}/tcomp-build.sh" "${workspace}/pom.xml"
              fi
              
              "${scripts_path}/server-registry-init.sh" "${download_dir}" "${install_dir}" \
                                                         "${runtimeVersion}" \
                                                         "1.36.0" "azure-dls-gen2" \
                                                         "${serverPort}"
              "${scripts_path}/server-registry-start.sh" "${install_dir}" "${serverPort}"
              "${scripts_path}/server-check.sh" "${serverPort}"
            """
          }
        }
      }
    }
    stage('Execute Test') {
      steps {
        withCredentials([nexusCredentials, tenantId_Rd]) {
          sh """
             printf "======== Execute test\n"
             cd ${tests_path}
             mvn clean test --settings="${MAVEN_SETTINGS}" \
                            -Dinstance="${tenantInstance}" \
                            -DaccountId="${componentRuntimeApiTenantId_Rd}" \
                            -DselectedEnvironment="${apiTesterEnv}" \
                            -DstopOnFailure=false \
                            -Dfile="${fileToRun}.json"
             """
        }
      }
    }
  }
  /**
   * post stages
   */
  post {
    always {

      script {
        sh """
             printf "======== Stop server\n"
             ${scripts_path}/server-registry-stop.sh ${install_dir}

             printf "======== Generate jacoco report \n"
             ${scripts_path}/jacoco-report.sh  ${install_dir}

             printf "======== Generate Html reports\n"
             mvn surefire-report:report-only -f ${tests_path}
             mvn site -DgenerateReports=false -f ${tests_path}
             """
      }

      recordIssues (
        enabledForFailure: true,
        tools: [
          taskScanner(
            id: 'todo-tck-api-test',
            name: 'api-test Todo(low)/Fixme(high)',
            includePattern: '**/src/it/web/test/**/*.json',
            ignoreCase: true,
            highTags: 'FIX_ME, FIXME',
            lowTags: 'TO_DO, TODO'
          ),
          junitParser(
            id: 'unit-test',
            name: 'tcomp API Test',
            pattern: '**/src/it/web/test/target/surefire-reports/**/*.xml'
          )
        ]
      )

      println "====== Archive artifacts"
      archiveArtifacts artifacts: "${artifact1}, ${artifact2}, ${artifact3}", allowEmptyArchive: false, onlyIfSuccessful: false
      sh """
        printf "Artifact 1: ${artifact1}\n"
        ls -l "${artifact1}"
        printf "Artifact 2: ${artifact2}\n"
        ls -l "${artifact2}"
        printf "Artifact 3: ${artifact3}\n"
        ls -l "${artifact3}"
        """

      println "====== Link API Coverage"
      publishHTML([
        allowMissing: false,
        alwaysLinkToLastBuild: false,
        keepAll: false,
        reportDir: "${install_dir}/coverage",
        reportFiles: 'index.html',
        reportName: 'API Coverage',
        reportTitles: 'index.html'
        ]
      )
    }
  }
}
